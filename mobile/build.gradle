// Gradle plugins
plugins {
    id 'com.adarshr.test-logger' version '1.1.2'
}
apply plugin: 'com.android.application'
apply plugin: 'jacoco'

// Jacoco Configurations
jacoco {
    toolVersion = "$jacocoVersion"
}

// Custom Jacoco test report script (including project build and testing)
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec", 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

// Custom Jacoco test report script (unit tests only)
task jacocoUnitTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

// Core Android builds
android {

    // Android SDK Version information
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    // Default Android SDK settings
    defaultConfig {
        applicationId "splitsound.com.splitsound"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Native programming build
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -frtti -fexceptions"
            }
        }
    }

    // Turn off animations for Unit testing
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR' // Use Orchestrator for instrumented tests
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }

    // Build configurations for debug and release
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Builds dummy cpp file
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    // Prevent build failures due to lint errors
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] }
    }
}

// Test logging
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed", "standardOut", "standardError"
        showStandardStreams true
    }

    jacoco.includeNoLocationClasses = true
}

// Imported dependencies
dependencies {
    repositories {
        mavenCentral()
    }

    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.google.android.gms:play-services-wearable:+'
    implementation 'com.android.support:appcompat-v7:27.1.1'

    // Never change Version numbers unless absolutely sure!

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'

    implementation files('libs/rtpLib.jar')

    testImplementation 'junit:junit:4.12'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.1"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:3.0.1") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    testImplementation 'org.robolectric:robolectric:3.8'

    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.felipecsl:gifimageview:2.1.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'commons-io:commons-io:2.4'

    wearApp project(':wear')

}
